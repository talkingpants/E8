let BrowserKB =
    DeviceTvmSoftwareVulnerabilitiesKB
    | where isnotempty(AffectedSoftware)
    | mv-expand s = AffectedSoftware
    | where s has_any ("Chrome","Edge","Firefox","Brave","Opera","Duck","Chromium","Safari","WebView2")
	| where isnotnull(PublishedDate) and PublishedDate <= ago(14d)
    | project CveId, IsExploitAvailable;
let BrowserVulnsPerDevice =
    DeviceTvmSoftwareVulnerabilities
    | join kind=inner BrowserKB on CveId
    | summarize CVEList = make_set(CveId, 10), VulnerabilityCount = dcount(CveId), ExploitAvailable = max(toint(IsExploitAvailable)) == 1 by DeviceId;
let Devices =
    DeviceInfo
    | summarize LastSeen = arg_max(Timestamp, *) by DeviceId
    | project DeviceId, DeviceName, OSPlatform, LastSeen;
Devices
| join kind=inner BrowserVulnsPerDevice on DeviceId
| project DeviceName, OSPlatform, LastSeen, VulnerabilityCount, ExploitAvailable, CVEList
| order by OSPlatform, ExploitAvailable, VulnerabilityCount desc

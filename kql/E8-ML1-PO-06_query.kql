let InternetFacingDevices =
    DeviceInfo
    | where IsInternetFacing == true
    | summarize arg_max(Timestamp, *) by DeviceId
    | project DeviceId, DeviceName, OSPlatform;
let DeviceVulns =
    DeviceTvmSoftwareVulnerabilities
    | project DeviceId, CveId, VulnerabilitySeverityLevel;
let CveMeta =
    DeviceTvmSoftwareVulnerabilitiesKB
    | where PublishedDate <= ago(14d)
    | project CveId, PublishedDate;
InternetFacingDevices
| join kind=inner DeviceVulns on DeviceId
| join kind=inner CveMeta     on CveId
| summarize VulnCount = dcount(CveId),
           VulnList  = make_list(CveId, 25)
           by DeviceName, OSPlatform
| order by VulnCount desc;